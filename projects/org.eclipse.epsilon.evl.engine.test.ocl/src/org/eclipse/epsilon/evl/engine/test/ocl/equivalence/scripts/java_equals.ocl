import java : '../metamodels/java.ecore'

package javaMM

context ClassDeclaration
	def: getPublicMethods : Collection(MethodDeclaration) = self.bodyDeclarations
		->selectByKind(MethodDeclaration)
		->select(md | md.modifier <> null and md.modifier.visibility = VisibilityKind::public)

	def: hasHashcode : Boolean = getPublicMethods->exists(method |
			method.name = 'hashCode' and
			method.parameters->isEmpty() and
			method.thrownExceptions->isEmpty() and
			method.returnType.type.oclIsTypeOf(PrimitiveTypeInt)
		)

	inv hasEquals: hasHashcode implies
		getPublicMethods->exists(method |
			method.name = 'equals' and
			method.parameters->size() = 1 and
			method.parameters->first().type.type.name = 'Object' and
			method.thrownExceptions->isEmpty() and
			method.returnType.type.oclIsTypeOf(PrimitiveTypeBoolean)
		)
	
	inv hasHashCode: hasHashcode
	
endpackage